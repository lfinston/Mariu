%% G.mf

%% * (1) Copyright and License.

%%%% This file is part of the source code for Mariu, a calligraphic font, programmed in METAFONT.
%%%% Copyright (C) 2022 Laurence D. Finston

%%%% Mariu is free software; you can redistribute it and/or modify 
%%%% it under the terms of the GNU General Public License as published by 
%%%% the Free Software Foundation; either version 3 of the License, or 
%%%% (at your option) any later version.  

%%%% Mariu is distributed in the hope that it will be useful, 
%%%% but WITHOUT ANY WARRANTY; without even the implied warranty of 
%%%% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the 
%%%% GNU General Public License for more details.  

%%%% You should have received a copy of the GNU General Public License 
%%%% along with Mariu; if not, write to the Free Software 
%%%% Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA 

%%%% Please send bug reports to Laurence.Finston@gmx.de

%%%% The author can be contacted at: 

%%%% Laurence D. Finston 
%%%% Laurence.Finston@gmx.de


%% This is G.mf
%% Using the right-hand drawing.

%% I wasn't able to get the crossing point at the lower right-hand corner
%% of the letter in the right place.


special "grayfont black";
beginchar("G",6.75pt#,7.5pt#,.25pt#); "The uncial G";
clear_vars;
letter_shift_string ="letter_shift (v0,.5pt,0);";
scaling_factor = 24/20;
nom_w = scaling_factor*4.9pt;
nom_h = scaling_factor*6.4pt;

%do_fill:=false;
do_fill:=true;

pickup my_fill_pen; 
%pickup tiny_pen; 

%% This does the main stroke, starting at about the middle
%% of the top, curving down and to the left to about the
%middle, and then curving toward the right continuing to the
%bottom at about the middle, then upwards, still to the right
%until about a quarter of the way up and then sharply to the
%left. 

%% There is a second, shorter stroke from the beginning of
%% the main stroke, downward and to the right, ending at the
%% right-hand border of the letter.

pos0(nib,42.5); pos1(nib,55);pos2(nib,50);
pos3(nib,50);pos4(nib,70);pos5(nib,70);

top z0r = (68/128 nom_w, 31/32nom_h);
lft z1l = (1/128 nom_w, 30/64 nom_h);
lft z2l = (3/128 nom_w, 4/16 nom_h);
bot lft z3l = (35/64 nom_w, -3/128 nom_h);
%%z4 = (59/64 nom_w, 14/64 nom_h);
z4 = (58/64 nom_w, 21/64 nom_h);
bot lft z5l = (1/2 nom_w, 19/64 nom_h);

begingroup save a,b,c,f,g,i,j,k,l,m,n,o;

%% Between z0 and z1
a = 1;
b = 1;
%% Between z1 and z2
c = 1;
f = 1;
%% Between z2 and z3
g = 1.1;
i = 1.1;
%% Between z3 and z4
j = .75;
k = 2;
%% Between z4 and z5
l = 1.3;
m = 1.5;

convert_path.p0
(z0e..tension a and b..z1e..%
tension c and f..z2e..%
tension g and i..z3e{dir -5}%
..tension j and k..z4e%
..tension l and m..z5e%
);

endgroup;

if do_fill=true:
pickup my_fill_pen; 
around_crossing_pt(p0,0,2,1.3x_pt_factor);
around_crossing_pt(p0,2,5,x_pt_factor);
fi;

%% This does the short diagonal stroke at the 
%% top of the G. 

pos6(nib,55);pos7(nib,60);

top rt z6r = (36/64 nom_w, nom_h);
top rt z7r = (nom_w, 55/64 nom_h);

convert_path.p1(z6e{dir -37.5}..{dir -15}z7e);

if do_fill=true:
pickup my_fill_pen; 
filldraw p1;
fi;

%do_letter_shift := false;
do_letter_shift := true;
if do_letter_shift = true:
do_rules := false;
scantokens letter_shift_string;
else:
penlabels(range 0 thru 7);
input nom_rules;
%input nom_rules_extra;
fi;

endchar;


% Local Variables:
% auto-save-interval:0
% End:





